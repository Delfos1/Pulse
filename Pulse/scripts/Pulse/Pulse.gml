enum PULSE_MODE
{
	OUTWARD				=	0,
	INWARD				=	1,
	SHAPE_FROM_POINT	=	2,
	SHAPE_FROM_SHAPE	=	3,
	NONE				=	4
}
enum PULSE_SHAPE
{
	POINT				=	10,
	SHAPE				=	11,
	A_TO_B				=	12,
	PATH				=	13
}


enum PULSE_RANDOM
{
	RANDOM				=	20,
	GAUSSIAN			=	21,
	EVEN				=	22,
	NONE				=	23,
}




function part_pulse_emitter(__part_system=__PULSE_DEFAULT_SYS_NAME,__part_type=__PULSE_DEFAULT_PART_NAME,__radius_external=50) constructor
{
	
	if  struct_exists(global.pulse.systems,__part_system)
	{
		_part_system = global.pulse.systems[$__part_system];
	}
	else
	{
		_part_system = pulse_make_system(__part_system);
	}

	if  struct_exists(global.pulse.part_types,__part_type)
	{
		_part_type = global.pulse.part_types[$__part_type];
	}
	else
	{
		_part_type = pulse_make_particle(__part_type);
	}

	_part_system		=	_part_system.index
	_index				=	_part_type._index
	
	//emitter form
	_path_a				=	undefined
	_path_b				=	undefined
	_path_res			=	-1
	_ac_channel_a		=	undefined
	_ac_channel_b		=	undefined
	_ac_tween			=	undefined
	_radius_external	=	abs(__radius_external)	
	_radius_internal	=	0
	_mask_start			=	0
	_mask_end			=	1
	
	//emitter properties
	x					=	0
	y					=	0
	_scalex				=	1
	_scaley				=	1
	_rot				=	270
	_mode				=	__PULSE_DEFAULT_EMITTER_MODE
	_dist_along_normal	=	__PULSE_DEFAULT_EMITTER_DISTRIBUTION
	_dist_along_form	=	__PULSE_DEFAULT_EMITTER_DIRECTION_DIST
	_revolutions		=	1
	_force_to_center	=	false
	_force_to_edge		=	false	
	
	
	
	_direction_range	=	[0,0]
	_speed_start		=	_part_type._speed
	_life				=	_part_type._life


	
	
	//Pulse properties settings
	
	static	stencil			=	function(__ac_curve,__ac_channel,_channel="a")
	{
		try
		{
			switch (_channel)
			{
				case "a":
					_ac_channel_a		=	animcurve_get_channel(__ac_curve,__ac_channel)
					break;
				case "b":
					_ac_channel_b		=	animcurve_get_channel(__ac_curve,__ac_channel)
					break;
			}
		}
		catch(_error)
		{
				show_debug_message("PULSE ERROR: Provided invalid Animation Curve")
		}
	}
	
	static	tween_stencil	=	function(__ac_curve_a,__ac_channel_a,__ac_curve_b,__ac_channel_b)
	{
		stencil(__ac_curve_a,__ac_channel_a,"a");
		stencil(__ac_curve_b,__ac_channel_b,"b");

		_ac_tween =	0;
	}
	
	static	mask			=	function(__mask_start,__mask_end)
	{
		if !is_real(__mask_start)	or !is_real(__mask_end){		show_debug_message("PULSE ERROR: Provided invalid input type (not a number)"); exit}
		_mask_start		=	__mask_start;
		_mask_end			=	__mask_end;
	}

	static	radius			=	function(__radius_internal,__radius_external)
	{
		_radius_internal	=	__radius_internal;
		_radius_external	=	__radius_external;
	}
	
	static	direction_range	=	function(_direction_min,_direction_max)
	{
		_direction_range	=	[_direction_min,_direction_max]

	}
	
	static	even_distrib	=	function(_angle=true,_length=true,__revolutions=1)
	{
		if _angle
		{
			_dist_along_form		= PULSE_RANDOM.EVEN
		}
		if _length
		{
			_dist_along_normal		= PULSE_RANDOM.EVEN	
		}
		_revolutions = __revolutions
	}

	static	transform		=	function(__scalex=_scalex,__scaley=_scaley,__rot=_rot)
	{
		_scalex			=	__scalex
		_scaley			=	__scaley
		_rot			=	__rot
	}
	
	static	draw_debug		=	function(x,y)
	{
		// draw radiuses
		var ext_x =  _radius_external*_scalex
		var int_x =  _radius_internal*_scalex
		var ext_y =  _radius_external*_scaley
		var int_y =  _radius_internal*_scaley
		draw_ellipse(x-ext_x,y-ext_y,x+ext_x,y+ext_y,true)
		draw_ellipse(x-int_x,y-int_y,x+int_x,y+int_y,true)
		
		//draw origin
		draw_line_width_color(x-10,y,x+10,y,1,c_green,c_green)
		draw_line_width_color(x,y-10,x,y+10,1,c_green,c_green)
		
		//draw angle
		/*
		
		var _anstart_x = x+lengthdir_x(ext_x,_mask_start)
		var _anstart_y = y+lengthdir_y(ext_y,_mask_start)
		var _anend_x = x+lengthdir_x(ext_x,_mask_end)
		var _anend_y = y+lengthdir_y(ext_y,_mask_end)
		draw_line_width_color(x,y,_anstart_x,_anstart_y,1,c_green,c_green)
		draw_line_width_color(x,y,_anend_x,_anend_y,1,c_green,c_green)
		*/
		// draw direction
		if _mode == PULSE_MODE.INWARD
		{
			draw_arrow(x+(ext_x/2),y,x,y,10)
		}
		else
		{
			draw_arrow(x,y,x+(ext_x/2),y,10)
		}
	}
	
	
	//Emit/Burst function 
	static	pulse		=	function(_amount,x,y)
	{
		if _path_res ==-1 && _path_a!= undefined
		{
			_path_res = power(path_get_number(_path_a)-1,path_get_precision(_path_a))
			
			if path_get_kind(_path_a) //SMOOTH
			{
				//
			}
			else
			{
				//_path_res = 1
			}
		}
		var rev,dir,dir_stencil,eval,eval_a,eval_b,length,_xx,_yy,i,j,x_origin,y_origin,x1,y1,normal,point,transv;
		var _speed_start	=	_part_type._speed
		var _life			=	_part_type._life
		
		point = _mask_start
		rev	= 1
		// KEEP ROTATION TRANSFORM WITHIN 360 ANGLE
		if  _rot>=360	_rot-=360

		repeat(_amount)
			{
				
				#region ASSIGN POINT 
				//Chooses a point at random along the form (number between 0 and 1)

				if _mask_start==_mask_end
				{
					point			=	_mask_end;
				}
				else if _dist_along_form	== PULSE_RANDOM.RANDOM
				{
					//Use random to distribute along 
					
					point			=	random_range(_mask_start,_mask_end);
				}
				else if _dist_along_form	== PULSE_RANDOM.GAUSSIAN
				{
					//Use gaussian to distribute the pointection along the mean
					//This doesnt work great
					
					var main=	(_mask_start+_mask_end)/2
					point			=	gauss(main,main/3);
				}
				else if _dist_along_form	== PULSE_RANDOM.EVEN
				{
					//Distribute the point evenly by particle amount and number of revolutions
					var _range = _mask_end-_mask_start
					point += (_range/_revolutions)
					
					if point >=_mask_end
					{
						point=_mask_start
					}
				}

				#endregion
				
				#region FORM (origin and normal)
				
				//If there is a path assigned, evaluate it.
				if		_path_a != undefined 
				{
					point		= random_range(_mask_start,_mask_end);
					j			= 1/_path_res
					x_origin	= path_get_x(_path_a,point)
					y_origin	= path_get_y(_path_a,point)
					x1			= path_get_x(_path_a,point+j)
					y1			= path_get_y(_path_a,point+j)
					transv		= point_direction(x_origin,y_origin,x1,y1)

					normal		= ((transv+90)>=360) ? transv-270 : transv+90;
				}
				//Otherwise pick the center of the emitter and convert point into a normal from center
				else
				{
					x_origin	=	x
					y_origin	=	y
					normal		=	point*360
				}
				
				#endregion

				#region DIRECTION (angle)
				//Adds a random range to the normal
				
				if _direction_range[0]!=_direction_range[1]
				{
					dir =	normal + random_range(_direction_range[0],_direction_range[1])
				}
				else
				{
					dir =	normal + _direction_range[0]
				}

				dir =  dir>=360 ? dir-360 : dir;

				#endregion

				#region STENCIL (animation curve)
				
				//If there is an animation curve channel assigned, evaluate it.
				
				eval		=	1;
				eval_a		=	1;
				eval_b		=	1;
				
				if _ac_channel_a !=undefined
				{
					dir_stencil = _rot+normal;
					dir_stencil=  dir_stencil>=360 ? dir_stencil-360 : dir_stencil
					
					if _ac_channel_b !=undefined
					{
						eval_a	=	animcurve_channel_evaluate(_ac_channel_a,dir_stencil*0.0028);
						eval_b	=	animcurve_channel_evaluate(_ac_channel_b,dir_stencil*0.0028);
						eval	=	lerp(eval_a,eval_b,abs(_ac_tween))
					}
					else
					{
						// IF THERE IS NO SHAPE B ASSIGNED
						
						if (_mode==PULSE_MODE.SHAPE_FROM_SHAPE)
						{
							_mode=PULSE_MODE.SHAPE_FROM_POINT
							show_debug_message("PULSE ERROR: Missing Shape B. Reverting mode to Shape from Point")
						}
						if _dist_along_normal == PULSE_SHAPE.A_TO_B
						{
							_dist_along_normal = PULSE_RANDOM.RANDOM
							show_debug_message("PULSE ERROR: Missing Shape B. Reverting Random Mode to Random")
						}
						eval	=	animcurve_channel_evaluate(_ac_channel_a,dir_stencil*0.0028);
					}
					
				}
				else if (_mode==PULSE_MODE.SHAPE_FROM_POINT or _mode==PULSE_MODE.SHAPE_FROM_SHAPE) && _ac_channel_b ==undefined
				{
					//IF THERE IS NO SHAPE A ASSIGNED
					
					_mode=PULSE_MODE.OUTWARD
					show_debug_message("PULSE ERROR: Missing Shape A. Reverting mode to Outward")
				}
				else if	_ac_channel_b !=undefined
				{
					show_debug_message("PULSE WARNING: Missing Shape A. Using Shape B instead")
					eval	=	animcurve_channel_evaluate(_ac_channel_b,dir_stencil*0.0028);
				}
				
				#endregion
				
				#region ASSIGN LENGTH (radius)
				
				if _radius_internal==_radius_external
				{
					// If the 2 radius are equal, then there is no need to randomize
					
					length		=	eval*_radius_external;
				}
				else if _dist_along_normal == PULSE_RANDOM.RANDOM
				{
					//Distribute along the radius by randomizing, then adjusting by shape evaluation
					
					length		=	eval*random_range(_radius_internal,_radius_external);
				}
				else if _dist_along_normal == PULSE_RANDOM.GAUSSIAN
				{
					//Distribute along the radius by gaussian random, then adjusting by shape evaluation
					//This doesnt work great
					
					length		=	eval*gauss(_radius_internal,_radius_external-_radius_internal)//,_radius_internal,_radius_external);
				}
				else if _dist_along_normal == PULSE_SHAPE.A_TO_B
				{
					//Distribute along the radius by randomizing, evaluating by each shape individually
					
					length		=	random_range(eval_a*_radius_internal,eval_b*_radius_external)
				}
				else if _dist_along_normal == PULSE_RANDOM.EVEN
				{
					//Distribute evenly
					
					length		=	eval*lerp(_radius_internal,_radius_external,(rev/_revolutions))
					rev++
					if rev>_revolutions rev=1
				}
				#endregion
			
			
				var _speed		=	random_range(_speed_start[0],_speed_start[1])
				var __life		=	random_range(_life[0],_life[1])
				var _accel		=	_speed_start[2]
	
			
				if abs(dir-normal)<=75			//NORMAL AWAY FROM CENTER
				{		
					var displacement = (_speed*__life)+(_accel*__life)
							
					if ((displacement < length) && _force_to_edge) or (displacement > length)
					{
						_speed	=	(length-(_accel*__life))/__life
					}

				}
	
				else if abs(angle_difference(dir,normal))<=105		//TRANSVERSAL
				{
						part_type_direction(_index,dir,dir,0,0)
				}
			
				else												//NORMAL TOWARDS CENTER
				{										
					var displacement = (_speed*__life)+(_accel*__life)
						
						
					if ((displacement < length) && _force_to_center) or (displacement > length)
					{
						_speed	=	(length-(_accel*__life))/__life
					}
				}
			
				
				part_type_life(_index,__life,__life);
				part_type_speed(_index,_speed,_speed,_accel,0)
				part_type_direction(_index,dir,dir,0,0)
				x_origin	+= (lengthdir_x(length,normal)*_scalex);
				y_origin	+= (lengthdir_y(length,normal)*_scaley);
				
				part_particles_create(_part_system, x_origin,y_origin,_index, 1);
				_part_type.reset()
			}
	}
}